//  Tutorial file schwarz.edp: Schwarz Algorithm
border a(t=0,1){x=t;y=0;} // This is a rectangle
border a1(t=1,2){x=t;y=0;}
border b(t=0,1){x=2;y=t;}
border c(t=2,0){x=t ;y=1;}
border d(t=1,0){x = 0; y = t;} // next is a circle
border e(t=0, pi/2){ x= cos(t); y = sin(t);}
border e1(t=pi/2, 2*pi){ x= cos(t); y = sin(t);}
real n=4;

//Omega1
mesh th = buildmesh( a(5*n) + a1(5*n) + b(5*n) + c(10*n) + d(5*n));
fespace Vh(th,P1); Vh v,u=0;

//Omega2
mesh TH = buildmesh ( e(5*n) + e1(25*n) );
fespace VH(TH,P1); VH V,U = 0;

for( int i=0; i< 4; i++) {
    plot(U,u, wait=1);
    solve AA(U,V)= int2d(TH)(dx(U)* dx(V)+ dy(U)* dy(V))
                 - int2d(TH)(V) + on(e,U=u)+ on(e1,U=0);
    solve aa(u,v)= int2d(th)(dx(u)*dx(v)+ dy(u)* dy(v))
                - int2d(th)(v) + on(a,d,u=U) + on(a1,b,c,u=0);
}
plot(U,u, wait=1);
