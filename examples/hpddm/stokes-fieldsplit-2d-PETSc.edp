//  run with MPI:  ff-mpirun -np 4 script.edp
// NBPROC 4

load "PETSc"                        // PETSc plugin
macro dimension()2// EOM            // 2D or 3D
include "macro_ddm.idp"             // additional DDM functions

macro def(i)[i, i#B, i#C]// EOM     // vector field definition
macro init(i)[i, i, i]// EOM        // vector field initialization
macro grad(u)[dx(u), dy(u)]// EOM   // two-dimensional gradient
real Sqrt = sqrt(2.);
macro epsilon(u)[dx(u), dy(u#B), (dy(u) + dx(u#B)) / Sqrt]// EOM
macro div(u)(dx(u) + dy(u#B))// EOM
func Pk = [P2, P2, P1];             // finite element space

int s = getARGV("-split", 1);                                   // refinement factor

if(verbosity > 0 && mpirank == 0) {
    cout << " --- " << mpirank << "/" << mpisize;
    cout << " - stokes-2d-PETSc.edp - input parameters: refinement factor = " << s << endl;
}

meshN Th = square(1, 1);
fespace Wh(Th, Pk);           // local finite element space
int[int][int] intersection;   // local-to-neighbors renumbering
real[int] D;                  // partition of unity
{
    meshN ThGlobal = square(getARGV("-global", 40), getARGV("-global", 40), [x, y]);    // global mesh
    ThGlobal = trunc(ThGlobal, (x < 0.5) || (y < 0.5), label = 5);
    Th = movemesh(ThGlobal, [-x, y]);
    Th = ThGlobal + Th;
    build(Th, s, intersection, D, Pk, mpiCommWorld)
}

varf vPb([u, uB, p], [v, vB, q]) = intN(Th)(grad(u)' * grad(v) + grad(uB)' * grad(vB) - div(u) * q - div(v) * p + 1e-10 * p * q) + on(1, 3, 5, u = 0, uB = 0) + on(2, u = y*(0.5-y), uB = 0);
matrix<real> K = vPb(Wh, Wh, tgv = -1);
real[int] rhs = vPb(0, Wh, tgv = -1);
Wh<real> def(b) = [1.0, 1.0, 2.0];
string[int] names(2);
names[0] = "velocity";
names[1] = "pressure";

Mat MatB(K, intersection, D);
set(MatB, prefix = "block_", sparams = "-block_pc_type fieldsplit -block_ksp_type fgmres -block_pc_fieldsplit_type multiplicative -block_pc_fieldsplit_detect_saddle_point -fieldsplit_velocity_ksp_type preonly"
+ " -fieldsplit_velocity_pc_type lu -fieldsplit_pressure_pc_type jacobi " 
+ " -fieldsplit_pressure_ksp_type gmres -block_ksp_monitor -block_ksp_view -block_ksp_rtol 1e-6", fields = b[], names = names);
b[] = 0.0;
b[] = MatB^-1 * rhs;
macro def2(u)[u, u#B]// EOM
macro def1(u)u// EOM
plotMPI(Th, def2(b), [P2, P2], def2, real, cmm = "Global velocity with fieldsplit preconditioner")
plotMPI(Th, bC, P1, def1, real, cmm = "Global pressure with fieldsplit preconditioner")
Mat MatM(MatB, K, clean = true);
set(MatM, prefix = "monolithic_", sparams = "-monolithic_pc_type lu -monolithic_ksp_view -monolithic_ksp_rtol 1e-6");
b[] = 0.0;
b[] = MatM^-1 * rhs;
plotMPI(Th, def2(b), [P2, P2], def2, real, cmm = "Global velocity with monolithic preconditioner")
plotMPI(Th, bC, P1, def1, real, cmm = "Global pressure with monolithic preconditioner")
