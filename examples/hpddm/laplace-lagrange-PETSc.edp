//  run with MPI:  ff-mpirun -np 4 script.edp
// NBPROC 4

load "PETSc"                        // PETSc plugin
macro dimension()2// EOM            // 2D or 3D
include "macro_ddm.idp"             // additional DDM functions

macro grad(u)[dx(u), dy(u)]// EOM   // two-dimensional gradient
func Pk = P1;                       // finite element space

meshN Th = square(getARGV("-global", 40), getARGV("-global", 40)); // global mesh
Mat A;
buildMat(Th, getARGV("-split", 1), A, Pk, mpiCommWorld)

fespace Wh(Th, Pk);                 // local finite element space
varf vPb(u, v) = intN(Th)(grad(u)' * grad(v));
func f = 1 + x - y;
varf vL(u, v)  = intN(Th)(f * v);
varf vb(u, v)  = intN(Th)(v);
matrix<real> Loc = vPb(Wh, Wh);
real[int] F = vL(0, Wh);
real[int] B = vb(0, Wh);
real[int] pB, pF, pSol;
A = Loc;
changeNumbering(A, B, pB);
changeNumbering(A, F, pF);

Mat N = [[ A  , pB ],
         [ pB', 0  ]];
if(mpirank == 0) {
    pF.resize(pF.n + 1);
    pF = [pF(0:pF.n - 2), 0];
    F.resize(F.n + 1);
    F = [F(0:F.n - 2), 0];
}
{
    set(N, sparams = "-pc_type lu");
    real[int] x = N^-1 * F;
    if(mpirank == 0)
        x.resize(x.n - 1);
    Wh sol;
    sol[] = x;
    macro params()cmm = "Global solution #1", wait = 1, dim = 3// EOM
    macro def(u)u//
    plotMPI(Th, sol, Pk, def, real, params)
}
{
    Mat C;
    MatConvert(N, C);
    set(C, sparams = "-pc_type lu");
    KSPSolve(C, pF, pSol);
    if(mpirank == 0)
        pSol.resize(pSol.n - 1);
    Wh sol;
    changeNumbering(A, sol[], pSol, inverse = true, exchange = true);
    macro params()cmm = "Global solution #2", wait = 1, dim = 3// EOM
    macro def(u)u//
    plotMPI(Th, sol, Pk, def, real, params)
}
{
    real[int] split(pF.n);
    split = 1;
    if(mpirank == 0)
        split(pF.n - 1) = 2;
    set(N, sparams = "-ksp_view_pre -pc_type fieldsplit -fieldsplit_0_pc_type lu -fieldsplit_0_mat_mumps_cntl_3 1e-12 -fieldsplit_0_mat_mumps_icntl_24 1 -pc_fieldsplit_detect_saddle_point", fields = split);
    KSPSolve(N, pF, pSol);
    if(mpirank == 0)
        pSol.resize(pSol.n - 1);
    Wh sol;
    changeNumbering(A, sol[], pSol, inverse = true, exchange = true);
    macro params()cmm = "Global solution #3", wait = 1, dim = 3// EOM
    macro def(u)u//
    plotMPI(Th, sol, Pk, def, real, params)
}
{
    Mat M = [[ 0 , pB'],
             [ pB, A  ]];
    set(M, sparams = "-pc_type lu");
    if(mpirank == 0) {
        real[int] tmp = F(0:F.n - 2);
        F(0) = 0;
        F(1:F.n - 1) = tmp;
    }
    cout << F<<endl;
    real[int] x = M^-1 * F;
    if(mpirank == 0) {
        real[int] tmp = x(1:F.n - 1);
        x.resize(x.n - 1);
        x = tmp;
    }
    Wh sol;
    sol[] = x;
    macro params()cmm = "Global solution #4", wait = 1, dim = 3// EOM
    macro def(u)u//
    plotMPI(Th, sol, Pk, def, real, params)
}
