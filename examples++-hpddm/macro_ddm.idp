load "thresholdings"
include "getARGV.idp"
IFMACRO(!partitioner)
macro partitioner()metis// EOM
ENDIFMACRO
IFMACRO(partitioner,metis)
load "metis"
macro partitionerSeq(part, Th, size)metisdual(part, Th, size)// EOM
macro partitionerPar(part, Th, comm, size)broadcast(processor(0, comm), part)// EOM
ENDIFMACRO
IFMACRO(partitioner,scotch)
load "scotch"
macro partitionerSeq(part, Th, size)scotch(part, Th, size)// EOM
macro partitionerPar(part, Th, comm, size)broadcast(processor(0, comm), part)// EOM
ENDIFMACRO
IFMACRO(partitioner,parmetis)
load "parmetis"
macro partitionerSeq(part, Th, size)// EOM
macro partitionerPar(part, Th, comm, size)parmetis(part, Th, size, communicator = comm, worker = getARGV("-parmetis_worker", 1))// EOM
ENDIFMACRO
IFMACRO(!partitionerSeq)
cout << "The macro 'partitioner' must be set to 'metis', 'scotch', or 'parmetis'" << endl;
exit(1);
ENDIFMACRO
IFMACRO(!dimension)
cout << "The macro 'dimension' must be defined" << endl;
exit(1);
ENDIFMACRO
IFMACRO(dimension,2)
macro meshN()mesh// EOM             // two-dimensional problem
macro bbN()bb2// EOM                // two-dimensional bounding box
macro intN()int2d// EOM             // two-dimensional integral
macro intN1()int1d// EOM            // one-dimensional integral
ENDIFMACRO
IFMACRO(dimension,3)
load "msh3"
macro meshN()mesh3// EOM            // three-dimensional problem
macro bbN()bb3// EOM                // three-dimensional bounding box
macro intN()int3d// EOM             // three-dimensional integral
macro intN1()int2d// EOM            // two-dimensional integral
ENDIFMACRO
IFMACRO(!meshN)
cout << "The macro 'dimension' must be set to '2' or '3'" << endl;
exit(1);
ENDIFMACRO
searchMethod = 1;

macro bb2(boundingMesh, overshoot, ThGlobal)
            real[int] bb(4);
            boundingbox(bb, boundingMesh);
            bb(0) -= overshoot;
            bb(1) += overshoot;
            bb(2) -= overshoot;
            bb(3) += overshoot;

            ThGlobal = trunc(ThGlobal, (x >= bb(0) && x <= bb(1) && y >= bb(2) && y <= bb(3)));// EOM

macro bb3(boundingMesh, overshoot, ThGlobal)
            real[int] bb(6);
            boundingbox(bb, boundingMesh);
            bb(0) -= overshoot;
            bb(1) += overshoot;
            bb(2) -= overshoot;
            bb(3) += overshoot;
            bb(4) -= overshoot;
            bb(5) += overshoot;

            ThGlobal = trunc(ThGlobal, (x >= bb(0) && x <= bb(1) && y >= bb(2) && y <= bb(3) && z >= bb(4) && z <= bb(5)));// EOM

macro plotMPI(Th, u, legend, Pk, def, K, d, w)
    if(mpirank == 0) {
        meshN ThCurrent = Th;
        meshN[int] meshTab(mpisize);
        fespace Xh(ThCurrent, Pk);
        Xh<K>[int] def(uTab)(mpisize);
        uTab[0][] = u;
        meshTab[0] = Th;
        mpiRequest[int] rq(mpisize - 1);
        for(int i = 1; i < mpisize; ++i)
            Irecv(processor(i, mpiCommWorld, rq[i - 1]), meshTab[i]);
        for(int i = 1; i < mpisize; ++i)
            mpiWaitAny(rq);
        for(int i = 1; i < mpisize; ++i) {
            ThCurrent = meshTab[i];
            Irecv(processor(i, mpiCommWorld, rq[i - 1]), uTab[i][]);
        }
        for(int i = 1; i < mpisize; ++i)
            mpiWaitAny(rq);
        plot(uTab, wait = w, nbiso = 40, fill = 1, value = 1, dim = d, cmm = legend);
    }
    else {
        mpiRequest[int] rq(2);
        Isend(processor(0, rq[0]), Th);
        Isend(processor(0, rq[1]), u);
        mpiWait(rq[0]);
        mpiWait(rq[1]);
    }// EOM

macro export(name, mesh, sol, fforder, comm)
load "iovtk"
savevtk(name + "_" + mpiRank(comm) + "_" + mpiSize(comm) + ".vtk", mesh, sol, order = fforder);

exec("pvpython vtk2vtu.py " + name + "_" + mpiRank(comm) + "_" + mpiSize(comm));
if(mpiRank(comm) == 0) {
    ofstream pvd(name + "_" + mpiSize(comm) + ".pvd");
    pvd << "<?xml version=\"1.0\"?>\n";
    pvd << "<VTKFile type=\"Collection\" version=\"0.1\"\n";
    pvd << "         byte_order=\"LittleEndian\"\n";
    pvd << "         compressor=\"vtkZLibDataCompressor\">\n";
    pvd << "  <Collection>\n";
    for(int i = 0; i < mpiSize(comm); ++i) {
        pvd << "    <DataSet timestep=\"0\" group=\"\" part=\"" << i << "\"\n";
        pvd << "             file=\""; pvd << name << "_" << i << "_" << mpiSize(comm) << ".vtu\"/>\n";
    }
    pvd << "  </Collection>\n";
    pvd << "</VTKFile>\n";
}
// EOM


macro partition(meshName, borderName, globalName, PhGlobal, VhGlobal, part, s, overlap, D, Wh, P, arrayIntersection, restrictionIntersection, comm, fakeInterface, PkPart, defPart, initPart)
        meshName = trunc(globalName, abs(part - mpiRank(comm)) < 0.1, label = fakeInterface);
        mpiBarrier(comm);
        real timerPartition = mpiWtime();
        {
            fespace PhLocal(meshName, P0);
            PhLocal overshoot = hTriangle;
            real max = 4.0 * overlap * overshoot[].max;
            bbN(meshName, max, globalName)
        }
        PhGlobal supp = abs(part - mpiRank(comm)) < 0.1;
        VhGlobal suppSmooth;
        AddLayers(globalName, supp[], 2 * overlap, suppSmooth[]);
        {
            meshN neighbors = trunc(globalName, suppSmooth > 0.001);
            fespace Oh(neighbors, P0);
            Oh partOverlap = part;
            Unique(partOverlap[], arrayIntersection, remove = mpiRank(comm));
        }
        fespace VhLocal(meshName, P1);
        VhLocal[int] partitionIntersection(arrayIntersection.n);
        if(s > 1) {
            globalName = trunc(globalName, suppSmooth > 0.001, split = s);
            supp = abs(part - mpiRank(comm)) < 0.1;
            suppSmooth = 0;
            AddLayers(globalName, supp[], 2 * overlap, suppSmooth[]);
        }
        globalName = trunc(globalName, suppSmooth > 0.001, label = 9999);
        real eps = intN(globalName)(1.0);
        real[int] epsTab(arrayIntersection.n);
        mpiRequest[int] rq(2 * arrayIntersection.n);
        for(int j = 0; j < arrayIntersection.n; ++j)
            Irecv(processor(arrayIntersection[j], comm, rq[j]), epsTab[j]);
        for(int j = 0; j < arrayIntersection.n; ++j)
            Isend(processor(arrayIntersection[j], comm, rq[arrayIntersection.n + j]), eps);
        supp = supp;
        suppSmooth = suppSmooth;
        meshName = trunc(globalName, suppSmooth > 0.501, label = fakeInterface);
        if(suppSmooth[].min < 0.501)
            borderName = trunc(globalName, (suppSmooth > (overlap - 0.999) / (2 * overlap)) && (suppSmooth < 0.501), label = (abs(fakeInterface) + 1) * 100);
        VhLocal khi = max(2 * suppSmooth - 1.0, 0.0);
        VhLocal sum = khi;
        VhGlobal phi = 0;
        part = part;
        int numberIntersection = 0;
        for(int k = 0; k < 2 * arrayIntersection.n; ++k)
            mpiWaitAny(rq);
        for(int i = 0; i < arrayIntersection.n; ++i) {
            PhGlobal suppPartition = abs(arrayIntersection[i] - part) < 0.1;
            AddLayers(globalName, suppPartition[], overlap, phi[]);
            if(min(eps, epsTab[i]) > 0.0) {
                real intersection = intN(globalName)(phi) / min(eps, epsTab[i]);
                if(intersection > 1.0e-6) {
                    partitionIntersection[numberIntersection] = phi;
                    if(usedARGV("-true_restrict") == -1)
                        sum[] += partitionIntersection[numberIntersection][];
                    arrayIntersection[numberIntersection++] = arrayIntersection[i];
                }
            }
        }
        if(numberIntersection != arrayIntersection.n)
            arrayIntersection.resize(numberIntersection);
        fespace WhPart(meshName, PkPart);
        WhPart defPart(func2vec);
        if(usedARGV("-true_restrict") == -1) {
            khi[] = khi[] ./= sum[];
            defPart(func2vec) = initPart(khi);
        }
        else {
            VhLocal loc = abs(part - mpiRank(comm)) < 0.1;
            defPart(func2vec) = initPart(loc);
        }
        D.resize(Wh.ndof);
        D = func2vec[];
        restrictionIntersection.resize(numberIntersection);
        for(int i = 0; i < numberIntersection; ++i) {
            meshN meshIntersection = trunc(meshName, partitionIntersection[i] > 1.0e-6, label = 9999);
            fespace WhIntersection(meshIntersection, P);
            matrix R = interpolate(WhIntersection, Wh);
            R.thresholding(1.0e-10);
            real[int] C;
            int[int] I;
            [I, restrictionIntersection[i], C] = R;
            restrictionIntersection[i].resize(R.nbcoef);
        }// EOM

macro buildEdgePeriodic(Th, ThBorder, ThGlobal, fakeInterface, s, overlap, D, arrayIntersection, restrictionIntersection, Wh, P, comm, excluded, PkPart, defPart, initPart, labPeriodic)
{
    real timing = mpiWtime();
    if(mpisize > 1 && !excluded) {
        fespace PhGlobal(ThGlobal, P0);
        fespace VhGlobal(ThGlobal, P1);
        PhGlobal part;
        timing = mpiWtime();
        meshN ThGlobalPeriodic;
        if(labPeriodic.n > 0) {
            VhGlobal marker;
            for(int i = 0; i < labPeriodic.n; ++i) {
                varf vMarker(u, v) = on(labPeriodic[i], u = 1.0);
                marker[] += vMarker(0, VhGlobal, tgv = -1);
            }
            PhGlobal partPeriodic = marker > 0.1;
            while(1) {
                AddLayers(ThGlobal, partPeriodic[], 1 + overlap, marker[]);
                partPeriodic = marker > 0.001;
                ThGlobalPeriodic = trunc(ThGlobal, partPeriodic < 0.999);
                if(ThGlobal.nt / real(ThGlobalPeriodic.nt) > mpisize / real(mpisize - 1))
                    break;
            }
        }
        if(mpiRank(comm) == 0) {
            if(verbosity > 0)
                cout.scientific << " --- global mesh of " << ThGlobal.nt << " elements (prior to refinement) partitioned with " << Stringification(partitioner);
            if(labPeriodic.n > 0) {
                fespace PhPeriodic(ThGlobalPeriodic, P0);
                PhPeriodic partPeriodic;
                if(mpiSize(comm) > 2) {
                    partitionerSeq(partPeriodic[], ThGlobalPeriodic, mpiSize(comm) - 1);
                    partPeriodic[] += 1.0;
                }
                else
                    partPeriodic[] = 1.0;
                part = partPeriodic;
            }
            else
                partitionerSeq(part[], ThGlobal, mpiSize(comm));
        }
        if(labPeriodic.n > 0 && Stringification(partitioner) != "metis" && Stringification(partitioner) != "scotch") {
            fespace PhPeriodic(ThGlobalPeriodic, P0);
            PhPeriodic partPeriodic;
            if(mpiSize(comm) > 2) {
                partitionerPar(partPeriodic[], ThGlobalPeriodic, comm, mpiSize(comm) - 1);
                partPeriodic[] += 1.0;
            }
            else
                partPeriodic[] = 1.0;
            part = partPeriodic;
        }
        else
            partitionerPar(part[], ThGlobal, comm, mpiSize(comm));
        if(mpiRank(comm) == 0 && verbosity > 0)
            cout.scientific << " (in " << mpiWtime() - timing << ")" << endl;
        timing = mpiWtime();
        partition(Th, ThBorder, ThGlobal, PhGlobal, VhGlobal, part, s, overlap, D, Wh, P, arrayIntersection, restrictionIntersection, comm, fakeInterface, PkPart, defPart, initPart)
    }
    else if(mpisize == 1) {
        Th = ThGlobal;
        D.resize(Wh.ndof);
        D = 1.0;
    }
    mpiBarrier(mpiCommWorld);
    if(verbosity > 0 && mpirank == 0) cout.scientific << " --- partition of unity built (in " << mpiWtime() - timing << ")" << endl;
}// EOM

macro buildEdge(Th, ThBorder, ThGlobal, fakeInterface, s, overlap, D, arrayIntersection, restrictionIntersection, Wh, P, comm, excluded, PkPart, defPart, initPart)
{
    int[int] emptyArray(0);
    buildEdgePeriodic(Th, ThBorder, ThGlobal, fakeInterface, s, overlap, D, arrayIntersection, restrictionIntersection, Wh, P, comm, excluded, PkPart, defPart, initPart, emptyArray)
}// EOM

macro build(Th, ThBorder, ThGlobal, fakeInterface, s, overlap, D, arrayIntersection, restrictionIntersection, Wh, P, comm, excluded)
{
    int[int] emptyArray(0);
    buildEdgePeriodic(Th, ThBorder, ThGlobal, fakeInterface, s, overlap, D, arrayIntersection, restrictionIntersection, Wh, P, comm, excluded, P, def, init, emptyArray)
}// EOM

macro buildPeriodic(Th, ThBorder, ThGlobal, fakeInterface, s, overlap, D, arrayIntersection, restrictionIntersection, Wh, P, comm, excluded, labPeriodic)
{
    buildEdgePeriodic(Th, ThBorder, ThGlobal, fakeInterface, s, overlap, D, arrayIntersection, restrictionIntersection, Wh, P, comm, excluded, P, def, init, labPeriodic)
}// EOM
