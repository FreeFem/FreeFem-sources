// file movemesh_rempli.edp
//load "glumesh3D"
load "msh3"
load "tetgen"

int C1=99; // could be anything

border C11(t=0,1){    x=t;     y=0.;  label=C1;}
border C12(t=0,1){   x=1.;      y=t;  label=C1;}
border C13(t=0,1){ x=1.-t;     y=1.;  label=C1;}
border C14(t=0,1){   x=0.;    y=1-t;  label=C1;}

mesh Th1 = buildmesh( C11(5)+C12(5)+C13(5)+C14(5) ); 

func ZZ1min = 0;
func ZZ1max = 1;
func XX1 = x;
func YY1 = y;

// build the differents face of cube

mesh3 Th31h = movemesh2D3Dsurf(Th1,transfo=[XX1,YY1,ZZ1max]);

mesh3 Th31b = movemesh2D3Dsurf(Th1,transfo=[XX1,YY1,ZZ1min]);

mesh3 Th32h = movemesh2D3Dsurf(Th1,transfo=[XX1,ZZ1max,YY1]);  

mesh3 Th32b = movemesh2D3Dsurf(Th1,transfo=[XX1,ZZ1min,YY1]);

mesh3 Th33h = movemesh2D3Dsurf(Th1,transfo=[ZZ1max,XX1,YY1]);  

mesh3 Th33b = movemesh2D3Dsurf(Th1,transfo=[ZZ1min,XX1,YY1]); 

mesh3 Th33 = Th31h+Th31b+Th32h+Th32b+Th33h+Th33b; // "gluing" surface meshs to obtain the surface of cube

mesh3 Thfinal = tetg(Th33,switch="pCQV");    // Tetrahelize the interior of the cube with tetgen


func XXm =2*x;
func YYm =y-1;
func ZZm =z*18+3;

mesh3 Th2final = movemesh3D(Thfinal,transfo=[XXm,YYm,ZZm]);
savemesh( Th2final,"remplissage_cube_move_final.mesh");
