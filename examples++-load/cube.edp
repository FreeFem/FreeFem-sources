//  august 2015 add 
load "msh3"
//  DEF LABEL unit CUBE before transformation 
//  6 face plan label are:
//  y = 0 ) label 1, 
//  x = 1 ) label 2, 
//  y = 1 ) label 3, 
//  x = 0 ) label 4, 
//  z = 0 ) label 5,
//  z = 1 ) label 6.
// the region numbering is 0 by default.

int[int] l6=[21,42,45,40,25,53];
int r11=11;
mesh3 Th=cube(4,5,6,[x*2-1,y*2-1,z*2-1],label=l6,flags =3,region=r11); 

// Check label dans  region numbering 
int err =0; 
for(int i=0; i<100; ++i)
{
    real s =int2d(Th,i)(1.);
    real sx=int2d(Th,i)(x);
    real sy=int2d(Th,i)(y);
    real sz=int2d(Th,i)(z);
    
    if( s )
    {
      int ix = sx/s+1, iy=sy/s+1, iz=sz/s+1, ii=(ix + 4*(iy+1) + 16*(iz+1) ) ;    
      //  value of ix,iy,iz =>  face min 0 ,  face max 2  , no face 1
      cout <<" label="<< i << " s " << s << " " << ix << iy << iz << " : " << ii << endl; 
      if( i != ii ) err++;
    }
}   
real volr11 = int3d(Th,r11)(1.) ;
cout << " vol region " << 11 << ": " << volr11 << endl; 
if( (volr11 - Th.mesure )>1e-8) err++;
plot(Th,fill=0); 
cout << " nb err= " << err <<endl;
assert(err==0); 
